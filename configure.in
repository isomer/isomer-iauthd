# $Id: configure.in,v 1.3 2008/01/20 02:31:13 kewlio Exp $

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(undernet-iauthd, 1.1)
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

AC_PROG_CC_C99
AC_PROG_CC_C_O
AC_PROG_CPP
AC_PROG_INSTALL
AC_ISC_POSIX
AC_HEADER_STDC
AC_CHECK_FUNCS(strsep)
AC_CHECK_HEADERS([netdb.h netinet/in.h stdlib.h string.h sys/socket.h])
AC_CHECK_HEADERS([arpa/inet.h stdbool.h])
# check for inet_addr - might need to check in -lnsl (e.g. solaris)
AC_CHECK_FUNCS(inet_addr, , AC_CHECK_LIB(nsl, inet_addr))
AC_CHECK_FUNCS(getaddrinfo, , AC_CHECK_LIB(socket, getaddrinfo))

# Check if ctime_r() takes two or three arguments
AC_CHECK_FUNCS(ctime_r)
if test "$ac_cv_func_ctime_r" = yes; then
	AC_MSG_CHECKING([if ctime_r() takes two arguments])
	AC_TRY_COMPILE([#include <time.h>],[
const time_t *t = 0;
char *buf;
ctime_r(t, buf);],
		AC_MSG_RESULT([yes]);
		AC_DEFINE(HAVE_CTIME_R_2,[1],[Define to 1 if the `ctime_r' function takes two parameters.]),
		AC_MSG_RESULT([no]))
fi

AC_ARG_WITH(debug, AC_HELP_STRING([--with-debug],[Enable debugging (default no)]),
	[AC_DEFINE(DEBUG,[1],[Define to 1 to enable full debugging])],
	[DEBUG=no])

AC_MSG_CHECKING(for perl regular expressions support)
AC_ARG_WITH(pcre, AC_HELP_STRING([--with-pcre],[Enable pcre support (default yes)]),
	[WITH_PCRE=$withval],[WITH_PCRE=yes])
AC_MSG_RESULT([$WITH_PCRE])

if test "x$cross_compiling" = xno -a "$WITH_PCRE" != "no"; then
	AC_PATH_PROG(PCRECONFIG, pcre-config)

	if test x"$PCRECONFIG" != x; then
		PCRE_LIB=`$PCRECONFIG --libs`
		CPPFLAGS="$CPPFLAGS `$PCRECONFIG --cflags`"
		OLDLIBS="$LIBS"
		LIBS="$LIBS $PCRE_LIB"
		AC_CHECK_LIB(pcre, pcre_compile, [
			AC_CHECK_HEADERS([pcre.h], [
				AC_DEFINE([HAVE_LIBPCRE], [1], [libpcre])
				AC_DEFINE([HAVE_PCRE_H], [1])
			])
		])
		LIBS="$OLDLIBS"
	else
		AC_MSG_ERROR([pcre-config wasn't found. Either install pcre and its headers or re-run with --without-pcre])
	fi
fi

AC_SUBST(PCRE_LIB)

if test "x${prefix}" = "xNONE"; then
  packageprefix=${ac_default_prefix}
else
  packageprefix=${prefix}
fi

AC_CONFIG_FILES([ Makefile src/Makefile include/iauthd_config.h 
])

AC_OUTPUT

echo ""
echo "Now type 'make' to build undernet-iauthd"

